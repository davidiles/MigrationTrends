

    model {

    # Trends in each region [r]
    mean.trend ~ dnorm(0,1)
    sd.trend.spatial ~ dunif(0,1)
    tau.trend.spatial <- pow(sd.trend.spatial, -2)

    logN0.sd ~ dnorm(0,10)
    for (r in 1:nregion){
      trend[r] ~ dnorm(mean.trend,tau.trend.spatial)
      #logN0.uncert[r] ~ dnorm(logN0[r], 5)
      logN0.uncert[r] <- logN0[r] + logN0.sd
    }

    proc.sd ~ dunif(0,1)
    proc.tau <- pow(proc.sd,-2)

    # True (unobserved) population dynamics in each region
    for (t in 1:T){
      for (r in 1:nregion){
        noise[r,t] ~ dnorm(0,proc.tau)
        logN[r,t] <- logN0.uncert[r] + trend[r] * (t-1) + noise[r,t] # Exponential population model
        N[r,t] <- exp(logN[r,t])
      }
    }

    # Proportion of birds from region [r] that are captured by station [s] (constant through time)
    for (r in 1:nregion){
      for (s in 1:nstation){
        rho[r,s] ~ dnorm(0,1) 
      }
    }

    # Observed numbers of birds at station [s] originating from region [r] in year [t]

      for (s in 1:nstation){
        for (t in 1:T){
          for (r in 1:nregion){

            mu[r,s,t] <- N[r,t] * rho[r,s]  # Seasonal total arriving at station [s] from region [r]  

          }

          # Total seasonal count at station [s]
          true.total[s,t] <- sum(mu[1:nregion,s,t]) 

          # Multinomial to describe regional composition in this year, at this station
          N.region.station[1:nregion,s,t] ~ dmulti(mu[,s,t], N.station.sampled[s,t])

        }
      }

    # Observed totals at each station each year
    for (s in 1:nstation){
      for (t in 1:T){

        obs.sd[s,t] <- true.total[s,t]*0.05  # Assume observed total is within 5% of true seasonal total
        obs.tau[s,t] <- pow(obs.sd[s,t],-2)

        # Observed seasonal total count
        N.station.total[s,t] ~ dnorm(true.total[s,t],obs.tau[s,t])  

      }
    }

    #Derived quantity: total birds across all regions
    for (t in 1:T){
      N.total[t] <- sum(N[,t])
    }

    }
    
