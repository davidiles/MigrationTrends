

    model {

    #---------------------------------------------
    # Model for population dynamics in each region
    #---------------------------------------------

    # Trends and initial abundance in each region [r]
    mean.trend ~ dnorm(0,1) # Grand mean of temporal trend
    sd.trend.spatial ~ dunif(0,1) # Spatial variance in trends
    tau.trend.spatial <- pow(sd.trend.spatial, -2)

    #logN0.sd <- 0.1
    #logN0.tau <- pow(logN0.sd,-2)
    
    for (r in 1:nregion){
      trend[r] ~ dnorm(mean.trend,tau.trend.spatial) # Trend in each region is estimated as a random effect

      #logN0.uncert[r] ~ dnorm(logN0[r],logN0.tau)    # Include uncertainty in initial abundance
      logN0.uncert[r] <- logN0[r] #~ dnorm(logN0[r],logN0.tau)    # No uncertainty in initial abundance
    }

    # Temporal variance in trend
    proc.sd ~ dunif(0,1)
    proc.tau <- pow(proc.sd,-2)

    # True (unobserved) population dynamics in each region
    for (t in 1:T){
      for (r in 1:nregion){
      
        logN[r,t] <- logN0.uncert[r] + trend[r] * (t-1) + noise[r,t] # Exponential population model
        noise[r,t] ~ dnorm(0,proc.tau)
        N[r,t] <- exp(logN[r,t])
      }
    }

    #---------------------------------------------
    # Observed numbers of birds at station [s] originating from region [r] in year [t]
    #---------------------------------------------

    # Proportion of birds from region [r] that are captured by station [s] (constant through time)
    for (r in 1:nregion){
      for (s in 1:nstation){
        rho[r,s] ~ dnorm(0,1) 
      }
    }

    for (s in 1:nstation){
      for (t in 1:T){
        for (r in 1:nregion){

          mu[r,s,t] <- N[r,t] * rho[r,s]  # Seasonal total arriving at station [s] from region [r]  

        }

        # Total seasonal count at station [s]
        true.total[s,t] <- sum(mu[1:nregion,s,t]) 

        # Multinomial to describe regional composition in this year, at this station
        N.region.station[1:nregion,s,t] ~ dmulti(mu[,s,t], N.station.sampled[s,t])

      }
    }

    #---------------------------------------------
    # Estimate totals at each station each year
    #---------------------------------------------

    mean.migrate.HYPER ~ dunif(0,nday)
    sd.migrate.HYPER ~ dunif(0,20)
    tau.migrate.HYPER <- pow(sd.migrate.HYPER,-2)

    daily.noise.sd ~ dunif(0,2)
    daily.noise.tau <- pow(daily.noise.sd,-2)

    for (s in 1:nstation){
      for (t in 1:T){

        mean.migrate[s,t] <- mean.migrate.HYPER
        sd.migrate[s,t] <- sd.migrate.HYPER

        for (d in 1:nday){

          norm.density[d,s,t] <- 1/(sqrt(2*pi)*sd.migrate[s,t])*exp(-((d-mean.migrate[s,t])^2/(2*sd.migrate[s,t]^2)))
              
          # Expected count on each day
          expected.count[d,s,t] <- norm.density[d,s,t] * true.total[s,t]
              
          # Daily observation error
          daily.noise[d,s,t] ~ dnorm(0,daily.noise.tau)
  
          lambda[d,s,t] <- exp(log(expected.count[d,s,t]) + daily.noise[d,s,t])
          daily.count[d,s,t] ~ dpois(lambda[d,s,t])

        }
      }
    }

    #---------------------------------------------
    # Derived quantities
    #---------------------------------------------

    # Total birds across all regions (used to determine national trend)
    for (t in 1:T){
      N.total[t] <- sum(N[,t])
    }

    }
    
